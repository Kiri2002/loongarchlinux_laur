# Contributor: taotieren <admin@taotieren.com>

pkgname=opengnb
pkgver=1.2.8.7
pkgrel=1
pkgdesc="GNB is open source de-centralized VPN to achieve layer3 network via p2p with the ultimate capability of NAT Traversal."
arch=('loong64' 'x86_64')
url="https://github.com/gnbdev/opengnb"
license=('GPLv3')
provides=(${pkgname})
conflicts=(${pkgname} ${pkgname}-git)
depends=('miniupnpc')
options=('!strip')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('e394f2bb5260708c19c54682fb32f4be9d54a0bcd98554bb7d0bb20388c70cf4')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make -f Makefile.linux
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make -f Makefile.linux install
    install -dm0755 "${pkgdir}/usr"
    cp -rv bin "${pkgdir}/usr"
    install -Dm644  "scripts/opengnb@.service" "${pkgdir}/usr/lib/systemd/system/opengnb@.service"
    install -Dm0644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    install -Dm644 man/gnb.8 "${pkgdir}/usr/share/man/man8/gnb.8"
    install -Dm644 man/gnb_ctl.8 "${pkgdir}/usr/share/man/man8/gnb_ctl.8"
    install -Dm644 man/gnb_es.8 "${pkgdir}/usr/share/man/man8/gnb_es.8"
    install -Dm644 man/gnb_crypto.1 "${pkgdir}/usr/share/man/man1/gnb_crypto.1"
}
